apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


android {

    compileSdkVersion Libs.build_versions_compile_sdk
    buildToolsVersion Libs.build_versions_build_tools

    defaultConfig {
        applicationId Libs.applicationId
        minSdkVersion Libs.build_versions_min_sdk
        targetSdkVersion Libs.build_versions_target_sdk
        versionCode Libs.versionCode
        versionName Libs.versionName
//        flavorDimensions Libs.versionCode
        multiDexEnabled true
//        multiDexKeepProguard file("tinkerMultidexKeep.pro") //keep specific classes using proguard syntax
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    signingConfigs {
        release {
            try {
                storeFile file("./sign/msc.keystore")
                storePassword "181207"
                keyAlias "msc"
                keyPassword "180507"
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def today = new Date()
            outputFileName = "com.msc.eyepetizer_${variant.buildType.name}_v${variant.versionName}_${today.format('yyyyMMdd')}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField "String", "BUGLY_APPID", "\"1a070e7f48\""
            signingConfig signingConfigs.release
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField "String", "BUGLY_APPID", "\"1a070e7f48\""
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

kapt {
//    generateStubs = true
    //ARouter
    arguments {
        arg("moduleName", project.getName())
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    if (isModule.toBoolean()) {
        implementation project(':lib_coremodel')
    } else {
        implementation project(':module_home_page')
        implementation project(':module_subscription')
        implementation project(':module_notification')
        implementation project(':module_me')
        implementation project(':module_launch')

    }

    //router
    implementation(Libs.arouter_runtime, { exclude group: 'com.android.support' })
    kapt Libs.arouter_compiler

    //tinker
    implementation(Libs.tinker_runtime) { changing = true }
    compileOnly(Libs.tinker_anno) { changing = true }
    kapt(Libs.tinker_compiler) { changing = true }

}

apply from: 'tinkerpatch.gradle'
